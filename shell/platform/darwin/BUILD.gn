# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_mac || is_ios)

import("$flutter_root/testing/testing.gni")
import("framework_shared.gni")

group("darwin") {
  if (is_ios) {
    deps = [
      "ios:flutter_framework",
    ]
  }
  if (is_mac) {
    deps = [
      "macos",
    ]
  }
}

source_set("flutter_channels") {
  sources = [
    "common/buffer_conversions.h",
    "common/buffer_conversions.mm",
    "ios/framework/Headers/FlutterBinaryMessenger.h",
    "ios/framework/Headers/FlutterChannels.h",
    "ios/framework/Headers/FlutterCodecs.h",
    "ios/framework/Headers/FlutterMacros.h",
    "ios/framework/Source/FlutterChannels.mm",
    "ios/framework/Source/FlutterCodecs.mm",
    "ios/framework/Source/FlutterStandardCodec.mm",
    "ios/framework/Source/FlutterStandardCodec_Internal.h",
  ]

  deps = [
    "$flutter_root/common",
    "$flutter_root/flow",
    "$flutter_root/fml",
    "$flutter_root/runtime",
    "$flutter_root/shell/common",
    "//third_party/skia",
  ]

  public_configs = [ "$flutter_root:config" ]
}

# Framework code shared between iOS and macOS.
source_set("framework_shared") {
  sources = [
    "ios/framework/Source/FlutterChannels.mm",
    "ios/framework/Source/FlutterCodecs.mm",
    "ios/framework/Source/FlutterStandardCodec.mm",
    "ios/framework/Source/FlutterStandardCodec_Internal.h",
  ]

  public = framework_shared_headers

  defines = [ "FLUTTER_FRAMEWORK" ]

  public_configs = [ "$flutter_root:config" ]
}

test_fixtures("flutter_channels_fixtures") {
  fixtures = []
}

executable("flutter_channels_unittests") {
  testonly = true

  sources = [
    "ios/framework/Source/flutter_codecs_unittest.mm",
    "ios/framework/Source/flutter_standard_codec_unittest.mm",
  ]

  deps = [
    ":flutter_channels",
    ":flutter_channels_fixtures",
    "$flutter_root/testing",
    "//third_party/dart/runtime:libdart_jit",
  ]

  public_configs = [ "$flutter_root:config" ]
}
